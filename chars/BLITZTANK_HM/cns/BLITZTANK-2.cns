[Statedef -2]

[State -2, クリップボード]
type = DisplayToClipboard
trigger1 = 1
text = "var(21)=%d, var(22)=%d, var(30)=%d, var(31)=%d,var(32)=%d \n"
params = var(21),var(22),var(30),var(31),var(32)

[State -2, クリップボード（追加専用）]
type = null;AppendToClipboard
trigger1 = 1
text = "fVar(20)=%f,fVar(10)=%f, fVar(11)=%f,fVar(12)=%f,fVar(21)=%f \n"
params = fVar(20),fVar(10),fVar(11),fVar(12),fVar(21)

[State -2, 常駐ヘルパー呼び出し]
type = Helper
trigger1 = NumHelper(46000) = 0
stateno = 46000
ID = 46000
keyctrl = 0
postype = p1
supermovetime = 99999999
pausemovetime = 99999999
ownpal = 1
IgnoreHitPause = 1

;いろいろ(変数)
[State -2,キャンセルフラグ初期化]
type = VarSet
trigger1 = var(17) != 0
trigger1 = Movetype != A
var(17) = 0

[State -2,汎用変数初期化]
type = VarRangeSet
triggerall = movetype != A
trigger1 = stateno != [190,195]
value = 0
first = 0
last = 4

[State -2,汎用変数初期化]
type = VarRangeSet
triggerall = movetype != A
trigger1 = stateno != [190,195]
fvalue = 0
first = 0
last = 4

[State -2 ,ダメージボイス1]
Type = PlaySnd
triggerall = Alive = 1
triggerall = Time = 1
triggerall = (StateNo = 5000 || StateNo = 5010 || StateNo = 5020)
trigger1 = Anim = 5000 || Anim = 5010 || Anim = 5020
Value = s101, 0
volume = 80
channel = 1
persistent = 0
ignorehitpause = 1

[State -2 ,ダメージボイス2]
Type = PlaySnd
triggerall = Alive = 1
triggerall = Time = 1
triggerall = (StateNo = 5000 || StateNo = 5010 || StateNo = 5020)
trigger1 = Anim = 5001 || Anim = 5011 || Anim = 5021
Value = s101, 1
volume = 80
channel = 1
persistent = 0
ignorehitpause = 1

[State -2 ,ダメージボイス3]
Type = PlaySnd
triggerall = Alive = 1
triggerall = Time = 1
trigger1 = (StateNo = 5000 || StateNo = 5010 || StateNo = 5020)
trigger1 = Anim = 5002 || Anim = 5012 || Anim = 5022
trigger2 = (StateNo = 5000 || StateNo = 5010 || StateNo = 5020)
trigger2 = Anim = 5030
Value = s101, 2
volume = 80
channel = 1
persistent = 0
ignorehitpause = 1

[State -2 ,やられボイス1]
Type = PlaySnd
triggerall = Alive = 0
triggerall = StateNo = 5150
trigger1 = Anim = 5140
trigger1 = Animelem = 2
Value = s102, 10
persistent = 0
ignorehitpause = 1

[State -2,受身可能フラグ初期化]
type = VarSet
trigger1 = var(30) != 0
trigger1 = Movetype != H
var(30) = 0

[State -2,受身失敗フラグ]
type = VarSet
triggerall = var(30) = 0
triggerall = Movetype = H
triggerall = stateno != 5100
trigger1 = (command = "a")
trigger2 = (command = "b")
trigger3 = (command = "c")
trigger4 = (command = "x")
trigger5 = (command = "y")
trigger6 = (command = "z")
var(30) = 1

;相対攻撃
[State -2,相対攻撃判定]
type = VarSet
triggerall = ishelper
triggerall = parent,StateNo != 3003
triggerall = parent,StateNo != 3013
triggerall = parent,StateNo != 3023
trigger1 = EnemyNear,MoveType = A
trigger1 = MoveType = A
var(15) = 1

[State -2,相対攻撃判定]
type = VarSet
triggerall = !ishelper
triggerall = StateNo != 3003
triggerall = StateNo != 3013
triggerall = StateNo != 3023
trigger1 = EnemyNear,MoveType = A
trigger1 = MoveType = A
var(15) = 1

[State -2,被相対攻撃判定]
type = VarSet
trigger1 = MoveType = A
trigger1 = EnemyNear,MoveType = A
var(16) = 1

[State -2,相対攻撃リセット]
type = VarSet
trigger1 = EnemyNear,ctrl
trigger2 = EnemyNear,MoveType = H
var(15) = 0

[State -2,被相対攻撃リセット]
type = VarSet
trigger1 = ctrl
trigger2 = MoveType = H
var(16) = 0
IgnoreHitPause = 1

;ヒット数カウント
[State -2,ヒット数]
type = VarAdd
trigger1 = MoveHit = 1
trigger2 = ProjHit = 1
var(12) = 1

[State -2,ヒット数初期化]
type = VarSet
triggerall = var(12) != 0
trigger1 = P2movetype != H
var(12) = 0

;補正色々
;攻撃関係
[State -2,コンボ補正1]
type = VarSet
trigger1 = var(12) = 1
fVar(10) = 0.75

[State -2,コンボ補正2]
type = VarSet
trigger1 = var(12) = 2
fVar(10) = 0.5

[State -2,コンボ補正3]
type = VarSet
trigger1 = var(12) >= 3
fVar(10) = 0.25

[State -2,相対攻撃補正]
type = VarSet
trigger1 = var(15) != 0
fVar(11) = 1.25

[State -2,背面ヒット補正]
type = VarSet
TriggerAll = enemynear,movetype != H
Trigger1 = (Facing = enemynear,Facing)
fVar(12) = 1.5

;計算式がめんどいので現在未実装
[State -2,体力差補正]
type = VarSet
Trigger1 = fVar(13) != 1
fVar(13) = 1

[State -2,コンボ補正リセット]
type = varset
triggerall = fVar(10) != 1
trigger1 = p2movetype != H
fVar(10) = 1

[State -2,相対攻撃補正リセット]
type = varset
triggerall = fVar(11) != 1
trigger1 = var(15) = 0
fVar(11) = 1

[State -2,背面ヒット補正リセット]
type = varset
triggerall = fVar(12) != 1
Trigger1 = (Facing != enemynear,Facing)
fVar(12) = 1

;防禦関係
[State -2,根性補正]
type = VarSet
trigger1 = Life <= 250
fVar(15) = 0.5+(Life/500)

[State -2,被相対攻撃補正]
type = VarSet
trigger1 = var(16) != 0
fVar(16) = 1.25

[State -2,被背面ヒット補正]
type = VarSet
Trigger1 = (Facing = enemynear,Facing)
fVar(17) = 1.5

;計算式がめんどいので現在未実装
[State -2,被体力差補正]
type = VarSet
Trigger1 = fVar(18) != 1
fVar(18) = 1

[State -2,根性補正リセット]
type = varset
triggerall = fVar(15) != 1
trigger1 = Life > 250
fVar(15) = 1

[State -2,被相対攻撃補正リセット]
type = varset
triggerall = fVar(16) != 1
trigger1 = Var(16) = 0
fVar(16) = 1

[State -2,被背面ヒット補正リセット]
type = varset
triggerall = fVar(17) != 1
Trigger1 = (Facing != enemynear,Facing)
fVar(17) = 1

;浮き関連
[State -2,浮き補正ヒット数]
type = VarAdd
triggerall = enemynear,StateType = A
trigger1 = MoveHit = 1
trigger2 = ProjHit = 1
var(18) = 1

[State -2,浮き補正ヒット数初期化]
type = VarSet
triggerall = var(18) != 0
trigger1 = enemynear,movetype != H
var(18) = 0

[State -2,浮き補正]
type = varset
trigger1 = Var(18) >= 4
fVar(20) = 0.50*(var(18))

[State -2,浮き補正リセット]
type = varset
triggerall = enemynear,movetype != H
trigger1 = var(18) = 0
fVar(20) = 0

;自分防禦ゲージ
[State -2,連続ガード補正リセット]
type = VarSet
trigger1 = MoveType != H
fvar(24) = 1

[State -2,立ちガークラゲージ蓄積]
type = VarAdd
triggerall = palno != 12
triggerall = stateno = 150
trigger1 = time = 0
var(24) = floor(ifelse((enemy,hitdefattr=SCA,HA,HP),50,ifelse((enemy,hitdefattr=SCA,SA,SP),30,10))*fvar(24))
ignorehitpause = 1

[State -2,屈みガークラゲージ蓄積]
type = VarAdd
triggerall = palno != 12
triggerall = stateno = 152
trigger1 = time = 0
var(24) = floor(ifelse((enemy,hitdefattr=SCA,HA,HP),50,ifelse((enemy,hitdefattr=SCA,SA,SP),30,10))*1.25*fvar(24))
ignorehitpause = 1

[State -2,空中ガークラゲージ蓄積]
type = VarAdd
triggerall = palno != 12
triggerall = stateno = 154
trigger1 = time = 0
var(24) = floor(ifelse((enemy,hitdefattr=SCA,HA,HP),50,ifelse((enemy,hitdefattr=SCA,SA,SP),30,10))*2*fvar(24))
ignorehitpause = 1

[State -2, 防禦回復]
Type = VarAdd
triggerall = var(24) > 0
triggerall = stateno != [4100,4101]
trigger1 = (GameTime % 30 = 0)
var(24) = -1
ignorehitpause = 1

[State -2,防禦崩壊]
type = Changestate
triggerall = palno != 12
triggerall = roundstate = 2
triggerall = stateno != [4100,4101]
trigger1 = var(24) >= 700
value = ifelse(StateType != A,4100,4101)

[State -2, 防禦崩壊フラグON]
Type = Varset
triggerall = palno != 12
triggerall = var(28) = 0
trigger1 = stateno = [4100,4101]
var(28) = 1
ignorehitpause = 1

[State -2, 防禦崩壊フラグOFF]
Type = Varset
triggerall = palno != 12
triggerall = var(28) != 0
trigger1 = stateno != [4100,4101]
trigger1 = movetype != H
var(28) = 0
ignorehitpause = 1

[State -2, 防禦ゲージリカバリ]
Type = Varset
trigger1 = var(28) != 0
trigger2 = RoundState = [0,1]
var(24) = 0
ignorehitpause = 1

[State -2, 防禦ゲージピンチ]
type = PalFX
triggerall = palno != 12
triggerall = var(24) > 500
triggerall = RoundState = 2
trigger1 = (GameTime % 20 = 0)
time = 10
add = 100,50,50
sinadd = 100,30,30,60
invertall = 0
IgnoreHitPause = 1
Persistent = 4


;━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━
;※※※特殊カラー※※※
;━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━
;11P黒カラー
;特攻ゲージ常時MAX
[State -2, パワーMAX]
Type = PowerSet
triggerall = palno = 11
trigger1 = alive
value = PowerMax
ignorehitpause = 1

;12P金カラー
;特攻ゲージ自動回復
;防御力10倍
;ギャラルホルン威力100倍

[State -2, 特攻ゲージ回復]
Type = PowerAdd
triggerall = palno = 12
trigger1 = alive
value = 1
ignorehitpause = 1


